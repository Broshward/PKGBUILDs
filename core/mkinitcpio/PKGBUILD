# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - swap zstd for gzip in depends, remove gzip optdepend
#  - patch to revert back to gzip for default compression
#  - patch for gzip kernel image support (https://github.com/archlinux/mkinitcpio/pull/86)

pkgname=mkinitcpio
pkgver=35.2
pkgrel=1.1
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url='https://gitlab.archlinux.org/archlinux/mkinitcpio/mkinitcpio'
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'binutils' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd')
makedepends=('asciidoc')
checkdepends=('bash-bats' 'bash-bats-assert' 'lzop')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('initramfs')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        '0001-use-gzip-for-compression-by-default.patch'
        '0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch'
        '0003-check-for-ALARM-kernels-in-run_post_hooks.patch')
install=mkinitcpio.install
sha512sums=('9ca8544bc5de3d215bf94ecf9a3bfb9265c9ed9871357af4b597a37e94c6951935641c11558470aaa934e8fbe9bc1f875dfa9d0fe8734a97f90154c9cbabfdba'
            'SKIP'
            'b243f29afd7d4e9863421c29dde4b1e1c30c8a45ed0cab5a2453686d5847cfd712d15eb082743fb537d2e2846aab65e8c7e55cce4365d276f2aee9c72a20789d'
            '58907a313e03256c852f22e1e41a08464f3bead58728d8b26a015a7996f4b1bfc554f7c44664e476ab186e4bf158b3374eb5315ea3653f19d1a1d5b2cf792a3d'
            'bfd5d9ac626c0d50736d76509db90386aa97616f1035e20370d682cf867e2599ed98b2772d13d7f6f7398c10972218148761a5860d0d24d3314e8235a22fa662')
b2sums=('e7d56ea1de8d21271d4e94a61563af9c5f33320a46ce9f006092bb70e2161be02791dfd75b14e9c9b5ad2fbb781617f04b4e49afa93a707785c23773c1a48664'
        'SKIP'
        '9d1d0dcb044613c34a2c758c422bccddcaa3dc97671c7522a3111e5ac34631f257df28ad65ba0cdaceae1b7676475146215fa5ccfc872e7ba7e73dce67fdb7f6'
        '43b60c230b0e992fe8a8539d45d7357e4306cb7e0f383188437b2b309e1bf38ebdcf0e41e683e5c53f8a5dc73a75e370b535d9726b48ec2564d22a42d14fd086'
        'e9086540eac07056c73486c4aad947029c412322c2f1a6a0c3ec146ed17363db59364ad96d863337f8e9308ff89419e6dfb82ab9a17efaa13132e5486b1b5b85')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB'    # Giancarlo Razzolini
              'C100346676634E80C940FB9E9C02FF419FECBE16')   # Morten Linderud

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-use-gzip-for-compression-by-default.patch
  patch -p1 -i ../0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch
  patch -p1 -i ../0003-check-for-ALARM-kernels-in-run_post_hooks.patch
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
