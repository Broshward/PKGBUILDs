# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix FTBFS on 32bit and gcc14

pkgname=cairo-perl
pkgver=1.109
pkgrel=6
pkgdesc="Perl wrappers for cairo"
arch=(x86_64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
options=('!emptydirs')
depends=('cairo' 'perl')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig')
source=(https://downloads.sourceforge.net/sourceforge/gtk2-perl/Cairo-${pkgver}.tar.gz
        Cairo-1.109.0-c99.patch)
sha512sums=('b80841bff480acf95f69659cba02ca17c79b67a2dc5c9b9302efb15bbd79bd4bc2f1801c001c220723da11081e86a08a83e07ecc8866810ebf9406e1f6fa1c53'
            '8ed038604771fdb409d3a72f07084a6310eff64f4295dfffcdaf3724e588521f4562a6149a169c17d9bedbe1ff5ec40744733224c47108a3b74124d64101ec3a')

build() {
  cd Cairo-$pkgver
  patch -p1 -i ../Cairo-1.109.0-c99.patch
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Cairo-$pkgver
  make test
}

package() {
  cd Cairo-$pkgver
  make DESTDIR="${pkgdir}" install
}
