# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - python2 variant that's still needed by packages

pkgname=python2-packaging
pkgver=20.9
pkgrel=7
pkgdesc="Core utilities for Python packages"
arch=('any')
url="https://github.com/pypa/packaging"
license=('Apache')
makedepends=('python2-setuptools' 'python2-pyparsing')
source=("https://github.com/pypa/packaging/archive/$pkgver/python-packaging-$pkgver.tar.gz"
        replace-distutils-usage-with-sysconfig.patch)
sha512sums=('fb71f1036cfaacbe94fdee663af31d6ad1960f73ecc95cba87b461c2d7d2ea90085853bb4682b146492d8c48f784b60ef082e3b1259269857166b143cd9a920b'
            '015ddcb799259190e3a4b97d386fb8e4cb6f76a22eed2ce97babf10116e886b82f6f3e3e74e3590dd14a8fce8e6ca5980a91205c61e29afa5dbdc387f4daa8dd')

prepare() {
  cd packaging-$pkgver
  patch -Np1 -i ../replace-distutils-usage-with-sysconfig.patch
}

build() {
  cd packaging-$pkgver
  python2 setup.py build
}

package() {
  depends=('python2-pyparsing' 'python2-six')

  cd packaging-$pkgver
  python2 setup.py install --root "$pkgdir"
}
