# Maintainer:
# Contributor: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - Redefined our CFLAGS and CXXFLAGS to remove -O2, and define -DNO_ASM.
#    Optimization kills the builds, NO_ASM prevents it using asm that doesn't work for us.
#  - Removed building and installing pdf docs, it always fails.

pkgname=cln
pkgver=1.3.7
pkgrel=2
pkgdesc="Class library for numbers"
arch=(x86_64)
url="https://www.ginac.de/CLN/"
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
  gmp
)
makedepends=('texlive-latex')
source=(https://www.ginac.de/CLN/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('7c7ed8474958337e4df5bb57ea5176ad0365004cbb98b621765bc4606a10d86b')

build() {
  cd ${pkgname}-${pkgver}
  if [ "$CARCH" = "aarch64" ]; then
    export CFLAGS="-march=armv8-a -pipe -DNO_ASM"
    export CXXFLAGS="-march=armv8-a -pipe -DNO_ASM"
  elif [ "$CARCH" = "armv7h" ]; then
    export CFLAGS="-march=armv7-a -mfloat-abi=hard -mfpu=vfpv3-d16 -pipe -DNO_ASM"
    export CXXFLAGS="-march=armv7-a -mfloat-abi=hard -mfpu=vfpv3-d16 -pipe -DNO_ASM"
  fi
  ./configure --prefix=/usr
  make all html
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install install-html
}
