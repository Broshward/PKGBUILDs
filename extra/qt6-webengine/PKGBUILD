# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - switch source to github
#  - patch to disable neon in libyuv, unsupported assembly
#  - patch for chromium GN
#  - patch for chromium to run blink bindings generation single threaded
#  - restrict job count on v7 - RAM constraints

highmem=1

pkgname=qt6-webengine
_pkgver=6.9.1
pkgver=${_pkgver/-/}
pkgrel=3
_chromium=636332b6a05ac3f78877509af61361e2cf126d99
arch=(x86_64)
url='https://www.qt.io'
license=(GPL-3.0-only
         LGPL-3.0-only
         LicenseRef-Qt-Commercial
         Qt-GPL-exception-1.0)
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=(alsa-lib
         dbus
         expat
         ffmpeg
         fontconfig
         freetype2
         gcc-libs
         glib2
         glibc
         harfbuzz
         icu
         lcms2
         libdrm
         libevent
         libjpeg-turbo
         libpng
         libtiff
         libwebp
         libx11
         libxcb
         libxcomposite
         libxfixes
         libxkbcommon
         libxkbfile
         libxdamage
         libxext
         libxml2
         libxrandr
         libxslt
         libxtst
         mesa
         minizip
         nspr
         nss
         openjpeg2
         opus
         qt6-base
         qt6-declarative
         qt6-positioning
         qt6-webchannel
         re2
         snappy
         ttf-font
         zlib)
       # pciutils
       # libvpx disabled if VA-API is used
makedepends=(cmake
             git
             gperf
             jsoncpp
             libepoxy
             libpulse
             libva
             libxcursor
             ninja
             nodejs
             perf
             pipewire
             python-html5lib
             qt6-tools
             qt6-websockets)
optdepends=('pipewire: WebRTC desktop sharing under Wayland')
groups=(qt6)
_pkgfn=${pkgname/6-/}
source=(git+https://github.com/qt/$_pkgfn#tag=v$pkgver
        git+https://github.com/qt/qtwebengine-chromium
        0001-disable-libyuv-neon.patch
        0001-ARM-toolchain-fixes.patch
        0002-Run-blink-bindings-generation-single-threaded.patch)
sha256sums=('299e854b5cf904c13e481908e264ad8a9fe86193d9dfefad4c842d1b8c695677'
            'SKIP'
            'a9dd73d2772b117aaae3ce9d4278796ddb3784131abfa78f1582d950518f72ce'
            '772aeba76d16387e28ef629285965109a4bfaa02ca2dd466e9526fe2030773ec'
            'eff4ebf2e714185ef6e36e30c8d2158da6b41a46e3168d06472d35f6c9977aaf')

prepare() {
  cd $_pkgfn
  git submodule init
  git submodule set-url src/3rdparty "$srcdir"/qtwebengine-chromium
  git -c protocol.file.allow=always submodule update

  patch -p1 -i ${srcdir}/0001-disable-libyuv-neon.patch

  # Fix rendering glitches (see https://bugreports.qt.io/browse/QTBUG-138641
  # and https://bugreports.qt.io/browse/QTBUG-139091).
  git cherry-pick -n 9dd5105673f30fa6e8dc9bb3ee5cf4f51cec6ed2

  # Bump chromium to head of stable branch
  cd src/3rdparty
  [[ -n $_chromium ]] && git checkout $_chromium || true

  if [[ $CARCH == "armv7h" ]]; then
    export MAKEFLAGS="-j1"
    export ALARM_NINJA_JOBS="1"
  fi
  sed -i 's/"-Wno-unused-function",/"-Wno-unused-function", "-Wno-incompatible-pointer-types",/' chromium/third_party/xnnpack/BUILD.gn
  patch -p1 -i ${srcdir}/0001-ARM-toolchain-fixes.patch
  patch -p1 -i ${srcdir}/0002-Run-blink-bindings-generation-single-threaded.patch
}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS \
    -DCMAKE_TOOLCHAIN_FILE=/usr/lib/cmake/Qt6/qt.toolchain.cmake \
    -DQT_FEATURE_webengine_system_ffmpeg=ON \
    -DQT_FEATURE_webengine_system_icu=ON \
    -DQT_FEATURE_webengine_system_libevent=ON \
    -DQT_FEATURE_webengine_system_re2=ON \
    -DQT_FEATURE_webengine_proprietary_codecs=ON \
    -DQT_FEATURE_webengine_kerberos=ON \
    -DQT_FEATURE_webengine_webrtc_pipewire=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$srcdir"/${_pkgfn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium
}
